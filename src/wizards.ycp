/*
|***************************************************************************
|
| Copyright (c) [2012] Novell, Inc.
| All Rights Reserved.
|
| This program is free software; you can redistribute it and/or
| modify it under the terms of version 2 of the GNU General Public License as
| published by the Free Software Foundation.
|
| This program is distributed in the hope that it will be useful,
| but WITHOUT ANY WARRANTY; without even the implied warranty of
| MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
| GNU General Public License for more details.
|
| You should have received a copy of the GNU General Public License
| along with this program; if not, contact Novell, Inc.
|
| To contact Novell about this file by physical or electronic mail,
| you may find current contact information at www.novell.com
|
|***************************************************************************
*/
/**
 * File:	include/iscsi-client/wizards.ycp
 * Package:	Configuration of iscsi-client
 * Summary:	Wizards definitions
 * Authors:	Michal Zugec <mzugec@suse.cz>
 *
 * $Id$
 */

{

textdomain "iscsi-client";

import "Sequencer";
import "Wizard";

include "iscsi-client/complex.ycp";
include "iscsi-client/dialogs.ycp";


/**
 * Main workflow of the iscsi-client configuration
 * @return sequence result
 */
any MainSequence() {

    /* FIXME: adapt to your needs */
    map aliases = $[
	"global"	:   ``( GlobalDialog() ),
	"disc_auth"	:   ``( DiscAuthDialog("client") ),
	"targets"	:   ``( TargetsDialog() ),
	"conn_auth"	:   ``( ConnAuthDialog("client") ),
	"conn_disc"	:   ``( ConnAuthDialog("discovered") ),
	"edit_conn"	:   ``( ConnAuthDialog("client") ),
	"disc"		:   ``( DiscAuthDialog("discovered") ),
    ];

    /* FIXME: adapt to your needs */
    map sequence = $[
	"ws_start" : "global",
	"global" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add  : "disc_auth",
	    `edit : "edit_conn",
	    `conn : "conn_disc",
	    `disc : "disc"
	],
	"disc_auth" : $[
	    `abort      : `abort,
            `back       : `back,
	    `next	: "targets"
	],
	"conn_disc" : $[
	   `abort	: `abort,
	   `back	: `back,
	   `next	: "global"
	],
	"targets" : $[
	    `abort	: `abort,
	    `back	: `back,
	    `next	: "global",
	    `conn_auth	: "conn_auth"
	],
	"conn_auth" : $[
	    `abort	: `abort,
	    `next	: "targets"
	],
	"edit_conn" : $[
	    `abort	: `abort,
	    `next	: "global"
	],
	"disc"		: $[
	    `abort	: `abort,
	    `back	: `back,
	    `next	: "global"
	]
    ];

    any ret = Sequencer::Run(aliases, sequence);

    return ret;
}

/**
 * Whole configuration of iscsi-client
 * @return sequence result
 */
any IscsiClientSequence() {

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::OpenCancelOKDialog();
    if (Mode::normal ())
	Wizard::SetDesktopTitleAndIcon("iscsi-client");

    any ret = Sequencer::Run(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of iscsi-client but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
any IscsiClientAutoSequence() {

    /* Initialization dialog caption */
    string caption = _("iSCSI Initiator Configuration");
    /* Initialization dialog contents */
    term contents = `Label(_("Initializing..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    any ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/* EOF */
}
